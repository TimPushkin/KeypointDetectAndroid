plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.gitlab.arturbosch.detekt'
}

android {
    compileSdk 32
    ndkVersion '25.1.8937393'

    defaultConfig {
        minSdk 21
        targetSdk 32

        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++11', '-frtti', '-fexceptions'
                arguments '-DSCAPIX_BRIDGE=java'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/cpp/generated/bridge/java'
        }
    }

    // Without this Kotlin code may start trying to compile before JNI interface is generated
    libraryVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def nativeBuild = tasks.getByName "externalNativeBuild$variantName"
        def compileKotlin = tasks.getByName "compile${variantName}Kotlin"

        compileKotlin.dependsOn nativeBuild
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'

    def pytorch_version = '1.12.2'
    implementation "org.pytorch:pytorch_android_lite:$pytorch_version"
    implementation "org.pytorch:pytorch_android_torchvision_lite:$pytorch_version"

    detektPlugins 'io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0'
}

detekt {
    config = files "$rootDir/config/detekt/detekt.yml"
    buildUponDefaultConfig = true
    ignoredBuildTypes = ['release']
}

// Exclude detekt from check task to make building independent from linting
tasks.named 'check' configure {
    dependsOn.removeIf { it.name == 'detekt' }
}
